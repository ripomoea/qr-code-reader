import versions.Versions
import dependencies.Dependencies

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.google.firebase.crashlytics'

apply from: rootProject.file('gradle/signing.gradle')

android {
    compileSdkVersion Versions.compileSdkVersion
    defaultConfig {
        applicationId "com.takechee.qrcodereader"
        minSdkVersion Versions.minSdkVersion
        targetSdkVersion Versions.targetSdkVersion
        versionCode Versions.versionCode
        versionName Versions.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        buildConfigField("String", "GIT_COMMIT_HASH", "\"${commitHash}\"")
    }
    buildTypes {
        debug {
            signingConfig = signingConfigs.getByName("debug")
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders = [crashlyticsEnabled: false]
        }
        staging {
            initWith(debug)
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
            manifestPlaceholders = [crashlyticsEnabled: true]
        }
        release {
            if (project.hasProperty('signingStoreFile')) {
                signingConfig = signingConfigs.getByName("release")
            }
            minifyEnabled false
            manifestPlaceholders = [crashlyticsEnabled: true]
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures.dataBinding = true

    testBuildType "staging"

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    // Required for AR because it includes a library built with Java 8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    // More than one file was found with OS independent path 'META-INF/LICENSE'
    // https://stackoverflow.com/a/47509465
    packagingOptions {
        resources {
            excludes += [
                    'META-INF/DEPENDENCIES',
                    'META-INF/LICENSE',
                    'META-INF/LICENSE.txt',
                    'META-INF/license.txt',
                    'META-INF/NOTICE',
                    'META-INF/NOTICE.txt',
                    'META-INF/notice.txt',
                    'META-INF/ASL2.0',
                    'META-INF/*.kotlin_module'
            ]
        }
    }
}

dependencies {
    implementation project(':core:core-component')
    implementation project(':feature:misc')
    implementation project(':model')
    implementation project(':legacy')
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation Dependencies.Kotlin.Coroutines.core
    implementation Dependencies.Kotlin.Coroutines.android

    implementation Dependencies.Jetpack.coreKtx
    implementation Dependencies.Jetpack.appcompat
    implementation Dependencies.Jetpack.fragment
    implementation Dependencies.Jetpack.constraintLayout
    implementation Dependencies.Jetpack.recyclerview
    implementation Dependencies.Jetpack.viewpager2
    implementation Dependencies.Jetpack.browser
    implementation Dependencies.Jetpack.preferenceKtx
    implementation Dependencies.Jetpack.material

    kapt Dependencies.Jetpack.Lifecycle.compiler
    implementation Dependencies.Jetpack.Lifecycle.liveDataKtx
    implementation Dependencies.Jetpack.Lifecycle.viewModelKtx
    implementation Dependencies.Jetpack.Lifecycle.savedState

    implementation Dependencies.Jetpack.Navigation.fragmentKtx
    implementation Dependencies.Jetpack.Navigation.uiKtx

    implementation Dependencies.Jetpack.Room.ktx
    implementation Dependencies.Jetpack.Room.runtime
    kapt Dependencies.Jetpack.Room.compiler
    testImplementation Dependencies.Jetpack.Room.testing

    implementation Dependencies.Jetpack.Licenses.core
    implementation Dependencies.Play.core

    implementation platform(Dependencies.Firebase.bom)
    implementation Dependencies.Firebase.analytics
    implementation Dependencies.Firebase.Crashlytics.core

    implementation Dependencies.Dagger.android
    implementation Dependencies.Dagger.androidSupport
    kapt Dependencies.Dagger.compiler
    kapt Dependencies.Dagger.processor

    implementation Dependencies.ZXing.core
    implementation Dependencies.ZXing.embedded

    implementation Dependencies.Permission.dispatcher
    kapt Dependencies.Permission.processor
    implementation Dependencies.Threeten.androidBackport

    testImplementation project(':core:test-component')
    testImplementation Dependencies.Test.kotlinTestAssertionsJvm
    testImplementation Dependencies.Threeten.core

    androidTestImplementation Dependencies.AndroidTest.junit
    androidTestImplementation Dependencies.AndroidTest.espresso
}

static def getCommitHash() {
    'git rev-parse --short HEAD'.execute().text.trim()
}

apply plugin: 'com.google.gms.google-services'
